MCSKIP MT,<>
MCSKIP + WITHS NL
MCSKIP *WITH* NL
MCINS %.
**
** remove NL
MCDEF RNL WITHS () AS <+
MCSET T2 = MCLENG(%A1.)
MCSET T1 = 1
%L2.+
MCGO L0 IF T1 GR T2
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <
>
MCSUB(%A1.,%T1.,%T1.)+
%L3.+
MCSET T1 = T1 + 1
MCGO L2
>
**
** remove SPACE TAB and NL
MCDEF RSPACES WITHS () AS <+
MCSET T2 = MCLENG(%A1.)
MCSET T1 = 1
%L2.+
MCGO L0 IF T1 GR T2
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = < >
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <	>
MCGO L3 IF MCSUB(%A1.,%T1.,%T1.) = <
>
MCSUB(%A1.,%T1.,%T1.)+
%L3.+
MCSET T1 = T1 + 1
MCGO L2
>
**
** decimal -> hex
MCDEF HD0  AS 0
MCDEF HD1  AS 1
MCDEF HD2  AS 2
MCDEF HD3  AS 3
MCDEF HD4  AS 4
MCDEF HD5  AS 5
MCDEF HD6  AS 6
MCDEF HD7  AS 7
MCDEF HD8  AS 8
MCDEF HD9  AS 9
MCDEF HD10 AS A
MCDEF HD11 AS B
MCDEF HD12 AS C
MCDEF HD13 AS D
MCDEF HD14 AS E
MCDEF HD15 AS F
MCDEF HDG WITHS () AS <+
MCDEF <_> AS HD%A1.
_>
MCDEF D2H WITHS () AS <+
MCSET T3 = %A1.
MCSET T1 = T3 / 16
MCSET T2 = T3 - T1 * 16
MCGO L1 IF %T1. EN 0
D2H(%T1.)+
%L1.HDG(%T2.)>
**
** hex -> decimal
MCDEF HV0 AS 0
MCDEF HV1 AS 1
MCDEF HV2 AS 2
MCDEF HV3 AS 3
MCDEF HV4 AS 4
MCDEF HV5 AS 5
MCDEF HV6 AS 6
MCDEF HV7 AS 7
MCDEF HV8 AS 8
MCDEF HV9 AS 9
MCDEF HVA AS 10
MCDEF HVB AS 11
MCDEF HVC AS 12
MCDEF HVD AS 13
MCDEF HVE AS 14
MCDEF HVF AS 15
MCDEF HVL WITHS () AS <+
MCDEF <_> AS HV%A1.
_>
MCDEF H2D WITHS () AS <+
MCGO L1 IF MCLENG(%A1.) GE 1
0+
MCGO L0
%L1.%H2D(MCSUB(%A1.,1,-1))*16+HVL(MCSUB(%A1.,0,0)).>
**
** maximum
MCDEF MAX WITHS (,) AS <+
MCGO L1 IF %A2. GE %A1.
%A1.MCGO L0
%L1.+
%A2.>
**
** field formatting
MCDEF FFL WITHS (,,) AS <+
MCSET T2 = MCLENG(%A3.)
MCSET T3 = MAX(%A2.,%T2.)
MCSET T1 = T3 - T2
%L2.MCGO L1 UNLESS %T1. GE 1
%A1.+
MCSET T1 = T1 - 1
MCGO L2 
%L1.%A3.>
**
MCDEF FFR WITHS (,,) AS <+
MCSET T2 = MCLENG(%A3.)
MCSET T3 = MAX(%A2.,%T2.)
MCSET T1 = T3 - T2
%A3.+
%L2.MCGO L1 UNLESS %T1. GE 1
%A1.+
MCSET T1 = T1 - 1
MCGO L2 
%L1.>
**
MCDEF FFC WITHS (,) AS <+
MCSET T1 = %A2.
%L2.MCGO L1 UNLESS %T1. GE 1
%A1.+
MCSET T1 = T1 - 1
MCGO L2
%L1.>
**
** LV & TLV
MCDEF LV WITHS { } AS <+
MCDEF <_> AS %B1.
MCDEF <$2> AS RSPACES(_)
MCSET T2 = MCLENG($2) / 2
FFL(0,2,D2H(%T2.))RNL(_)>
MCDEF LV NL AS <+
MCDEF <_> AS %B1.
MCDEF <$2> AS RSPACES(_)
MCSET T2 = MCLENG($2) / 2
FF(0,2,D2H(%T2.))RNL(_)
>
MCDEF TLV WITHS () WITHS { } AS <%A1.LV{%B2.}>
**
**
MCDEF OPT SEL OR SEL WITHS [] ALL NL AS <+
MCGO LT1
%L1.MCDEF <_> AS 0004
MCGO L3
%L2.MCDEF <_> AS %B1.
%L3.+
cmd: 00A4 _ LV{%BT1.}
rsp: .*
>
**
**
MCDEF OPT RDB WITHS () WITHS NL OR RDB WITHS [] WITHS () WITHS NL ALL AS <+
MCGO LT1
%L1.+
MCDEF P1P2 AS 0000
MCDEF Le AS %A1.
MCGO L3
%L2.+
MCDEF P1P2 AS %A1.
MCDEF Le AS %A2.
%L3.+
cmd: 00B0 P1P2 Le
rsp: .*
>
**
**
MCDEF OPT UDB OR UDB WITHS [ OPT ] OR ] WITHS () ALL OR UDB WITHS () ALL NL AS <+
MCGO LT1
%L1.+
MCDEF P1P2 AS 0000
MCDEF Data AS %B1.
MCGO L4
%L2.+
MCGO L21 IF MCSUB(%WD0.,0,0) = [
MCDEF P1P2 AS 0000
MCSET T1 = MCLENG(RSPACES(%B2.))
MCDEF Data AS %B2. FFC(F,%H2D(%A1.)*2-T1.)
MCGO L4
%L21.+
MCDEF P1P2 AS %A1.
MCDEF Data AS %B2.
MCGO L4
%L3.+
MCDEF P1P2 AS %A1.
MCSET T1 = MCLENG(RSPACES(%B3.))
MCDEF Data AS %B3. FFC(F,%H2D(%A2.)*2-T1.)
%L4.+
cmd: 00D6 P1P2 LV{Data}
rsp: .*
>
**
**
MCDEF OPT RDR WITHS () WITHS NL OR RDR WITHS [] WITHS () WITHS NL ALL AS <+
MCGO LT1
%L1.+
MCDEF P1P2 AS 0002
MCDEF Le AS %A1.
MCGO L3
%L2.+
MCDEF P1P2 AS %A1.
MCDEF Le AS %A2.
%L3.+
cmd: 00B2 P1P2 Le
rsp: .*
>
**
**
MCDEF OPT UDR OR UDR WITHS [ OPT ] OR ] WITHS () ALL OR UDR WITHS () ALL NL AS <+
MCGO LT1
%L1.+
MCDEF P1P2 AS 0002
MCDEF Data AS %B1.
MCGO L4
%L2.+
MCGO L21 IF MCSUB(%WD0.,0,0) = [
MCDEF P1P2 AS 0002
MCSET T1 = MCLENG(RSPACES(%B2.))
MCDEF Data AS %B2. FFC(F,%H2D(%A1.)*2-T1.)
MCGO L4
%L21.+
MCDEF P1P2 AS %A1.
MCDEF Data AS %B2.
MCGO L4
%L3.+
MCDEF P1P2 AS %A1.
MCSET T1 = MCLENG(RSPACES(%B3.))
MCDEF Data AS %B3. FFC(F,%H2D(%A2.)*2-T1.)
%L4.+
cmd: 00DC P1P2 LV{Data}
rsp: .*
>
**
**
MCDEF VRF WITHS [] NL AS <+
cmd: 0020 %A1. 08 %B2.
rsp: .*
>
**
**
MCDEF GET WITHS () WITHS NL AS <+
cmd: 00C0 0000 %A1.
rsp: .*
>
**
**
MCDEF CRT NL AS <+
cmd: 00E0 0000 LV TLV(62){%B1.}
rsp: .*
>
**
**
MCDEF DLT NL AS <+
cmd: 00E4 0000 LV{%B1.}
rsp: .*
>
**
**
MCDEF fcpFD WITHS { } AS <TLV(82){%B1.}>
MCDEF fcpFI WITHS { } AS <TLV(83){%B1.}>
MCDEF fcpLCSI WITHS { } AS <TLV(8A){%B1.}>
MCDEF fcp8B WITHS { } AS <TLV(8B){%B1.}>
MCDEF fcpFS WITHS { } AS <TLV(80){%B1.}>
MCDEF fcpSFI WITHS { } AS <TLV(88){%B1.}>
MCDEF fcpPI WITHS { } AS <TLV(A5){%B1.}>
MCSET S16 = 9
MCSET S17 = 32
